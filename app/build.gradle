evaluationDependsOn(':server')

apply plugin: 'com.android.application'

android {
    namespace 'xyz.aicy.scrcpy'
    flavorDimensions = ["default"]

    compileSdkVersion 33
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        versionCode version_code
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        setProperty("archivesBaseName", "scrcpy")
        
        // Enable 16KB page alignment for native libraries
        ndk {
            // This ensures proper alignment
        }
        versionName app_version
    }
    signingConfigs {
        release {
            storeFile file("scrcpy.jks")

            def properties = new Properties()
            if (file("../local.properties").exists()){
                file("../local.properties").withInputStream { stream ->
                    properties.load(stream)
                }
            }

            storePassword System.getenv("SIGNING_STORE_PASSWORD") ?: properties.getProperty("SIGNING_STORE_PASSWORD")
            keyAlias System.getenv("SIGNING_KEY_ALIAS") ?: properties.getProperty("SIGNING_KEY_ALIAS")
            keyPassword System.getenv("SIGNING_KEY_PASSWORD") ?: properties.getProperty("SIGNING_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }

    packaging {
        jniLibs {
            useLegacyPackaging = true
        }
    }

    productFlavors {
        scrcpy {
            applicationId "xyz.aicy.scrcpy"
        }
    }


    applicationVariants.all { variant ->
        variant.preBuildProvider.configure {
            dependsOn(":server:copyServer")
        }
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.6.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
